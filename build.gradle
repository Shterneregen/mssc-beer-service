plugins {
    id "java"
    id "org.springframework.boot" version "2.3.0.RELEASE"
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id "net.ltgt.apt" version "0.20"
}

ext {
    mapstructVersion = "1.3.1.Final"
    mockitoVersion = "3.3.3"
    mysqlConnectorJavaVersion = "8.0.16"
    springCloudVersion = "Hoxton.SR6"
}

group = "guru.springframework"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = "1.8"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(
            "org.springframework.boot:spring-boot-starter-actuator",
            "org.springframework.boot:spring-boot-starter-artemis",
            "org.springframework.boot:spring-boot-starter-cache",
            "org.springframework.boot:spring-boot-starter-data-jpa",
            "org.springframework.boot:spring-boot-starter-validation",
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client",
            "javax.cache:cache-api",
            "org.ehcache:ehcache",
            "org.mapstruct:mapstruct:$mapstructVersion"
    )
    compileOnly "org.projectlombok:lombok"
    developmentOnly "org.springframework.boot:spring-boot-devtools"
    runtimeOnly(
            "org.springframework.boot:spring-boot-devtools",
            "com.h2database:h2",
            "mysql:mysql-connector-java:$mysqlConnectorJavaVersion"
    )
    annotationProcessor(
            "org.projectlombok:lombok",
            "org.mapstruct:mapstruct-processor:$mapstructVersion"
    )
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}
